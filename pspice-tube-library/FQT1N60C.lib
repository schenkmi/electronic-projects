**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FQT1N60C
** 600V N-Channel MOSFET and SOT-223
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FQT1N60C 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 660
Lgate 1 9    4.989e-9
Ldrain 2 5   1.44e-9
Lsource 3 7  2.49e-9
RLgate 1 9   4.89
RLdrain 2 5  10.4
RLsource 3 7 10.9
Rgate 9 6    1.4
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.18e-3 TC2=-1.25e-6)
.MODEL DbodyMOD D (IS=6.5e-10  N=1.5    RS=9.12e-2   TRS1=1.0e-3 TRS2=9.2e-7 
+ CJO=1.40e-10     M=0.68      VJ=0.64  TT=6.58e-7   XTI=3       EG=1.12)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 9.165
.MODEL RdrainMOD RES (TC1=8.0e-3 TC2=2.22e-5)
M_BSIM3 16 6 7 7 Bsim3 W=0.232 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=980e-10     XJ=1.4e-6      NCH=1.3e17
+ U0=700          VSAT=1.5e5     DROUT=1.0
+ DELTA=0.1       PSCBE2=0       RSH=1.09e-3
+ VTH0=3.30       VOFF=-0.1      NFACTOR=1.1
+ LINT=1.48e-7    DLC=1.48e-7    FC=0.5
+ CGSO=6.55e-12   CGSL=0         CGDO=8.05e-12  
+ CGDL=5.46e-10   CJ=0           CF=0
+ CKAPPA=0.2      KT1=-1.57      KT2=0
+ UA1=2.0e-10     NJ=10)
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** 600V N-Channel MOSFET and SOT-223
**-------------------------------------------------------------------------------
.SUBCKT FQT1N60C_Thermal TH TL
CTHERM1 TH 6 3.240e-4
CTHERM2 6 5  2.360e-3
CTHERM3 5 4  2.980e-3
CTHERM4 4 3  5.520e-3
CTHERM5 3 2  1.820e-2
CTHERM6 2 TL 3.020e-1
RTHERM1 TH 6 1.320e-1
RTHERM2 6 5  4.980e-1
RTHERM3 5 4  6.790e-1
RTHERM4 4 3  1.085e+0
RTHERM5 3 2  4.443e+0
RTHERM6 2 TL 7.053e+0
.ENDS FQT1N60C_Thermal 
**-------------------------------------------------------------------------------
** Creation: Oct-07-2010   Rev.: 1.0
** Fairchild Semiconductor

